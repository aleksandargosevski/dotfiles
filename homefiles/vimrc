let data_dir = has('nvim') ? stdpath('data') . '/site' : '~/.vim'
if empty(glob(data_dir . '/autoload/plug.vim'))
  silent execute '!curl -fLo '.data_dir.'/autoload/plug.vim --create-dirs  https://raw.githubusercontent.com/junegunn/vim-plug/master/plug.vim'
  autocmd VimEnter * PlugInstall --sync | source $MYVIMRC
endif

call plug#begin('~/.vim/plugged')

" Files buffers navigation
Plug 'scrooloose/nerdtree', { 'on': ['NERDTreeToggle', 'NERDTreeFind'] }
Plug 'ryanoasis/vim-devicons'
Plug 'PhilRunninger/nerdtree-visual-selection'
Plug 'junegunn/fzf', { 'dir': '~/.fzf', 'do': './install --all' }
Plug 'junegunn/fzf.vim'
Plug 'christoomey/vim-tmux-navigator'

" Search and movement
Plug 'mileszs/ack.vim'
Plug 'embear/vim-foldsearch', { 'on': ['Fs'] }
Plug 'easymotion/vim-easymotion'
" Git and command line
Plug 'tpope/vim-fugitive'

" Autocomplete
Plug 'Shougo/deoplete.nvim'
Plug 'roxma/nvim-yarp'
Plug 'roxma/vim-hug-neovim-rpc'
let g:deoplete#enable_at_startup = 1

" Code edit
Plug 'mattn/emmet-vim'
Plug 'AndrewRadev/splitjoin.vim'
Plug 'dense-analysis/ale'
Plug 'AndrewRadev/tagalong.vim'
Plug 'kana/vim-textobj-user' | Plug 'whatyouhide/vim-textobj-xmlattr'
Plug 'cohama/lexima.vim'
Plug 'deathlyfrantic/lexima-template-rules'
Plug 'tpope/vim-surround'
Plug 'tpope/vim-repeat'
Plug 'tpope/vim-commentary'
Plug 'editorconfig/editorconfig-vim'

" Notes
Plug 'vimwiki/vimwiki'
Plug 'junegunn/goyo.vim'

" Color schemes
Plug 'danielwe/base16-vim'

" Add plugins to &runtimepath
call plug#end()

" ================================================
" Color scheme
" ================================================
let base16colorspace=256
set background=dark
execute "colorscheme base16-".$THEME

" ================================================
" General config
" ================================================
set relativenumber              "Line numbers are good
set number                      "Line numbers are good
set binary                      "don't add new line on end of file (git)
set gdefault                    "Set global flag for search and replace
set cursorline                  "Highlight row
set autoread                    "Reload files changed outside vim
set smartcase                   "Smart case search if there is uppercase
set ignorecase                  "case insensitive search
set hlsearch                    "highlight search results
set incsearch                   "show matches in real time
set clipboard+=unnamed          "use system clipboard
set fileencoding=utf-8          "Set utf-8 encoding on write
set encoding=utf-8              "for vim
set laststatus=0
set re=2                        "Faster vim with color schemes
set ruler                       "Show line/column number
set hidden                      "Switch buffers when they are not saved"
set belloff=all                 "turn off bells
set mouse=a                     "enable mouse
set lazyredraw                  "lazy redraw for macors
set timeoutlen=1000             "Fast escape
set ttimeoutlen=0               "Fast escape
set foldcolumn=0                "Hide fold column
set diffopt+=vertical           "Vertical diff
" set listchars=eol:¬,tab:>·,trail:~,extends:>,precedes:<,space:·
" set list
" set cursorcolumn                "Highlight cursor column
syntax on                       "Turn on syntax highlighting

" ================================================
" Colors
" ================================================
highlight clear FoldColumn                     "Use same color for padding as background
highlight LineNr ctermfg=19 ctermbg=NONE         "Change color of line numbers
highlight CursorLineNr ctermfg=20 ctermbg=19     "Change color of line numbers
" highlight CursorLine ctermbg=18                 "Current line on
" highlight CursorColumn ctermbg=18                 "Current line on
highlight CursorLine ctermbg=NONE                 "Current line highlight off
highlight VertSplit ctermbg=NONE ctermfg=NONE    "Remove border background
highlight EndOfBuffer ctermfg=NONE ctermbg=NONE  "Color of ~ at end of the file
highlight SignColumn ctermbg=NONE                "Color of sign column (linter)
highlight Comment cterm=italic
highlight Keyword cterm=italic
highlight Special cterm=italic
highlight Statement cterm=italic
highlight Exception cterm=italic
highlight Function cterm=italic
hi clear CursorLineNR
" hi clear CursorLine
hi Normal ctermbg=NONE guibg=NONE

" ================================================
" Cursor style in tmux
" ================================================
let &t_SI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=1\x7\<Esc>\\"
let &t_SR = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=2\x7\<Esc>\\"
let &t_EI = "\<Esc>Ptmux;\<Esc>\<Esc>]50;CursorShape=0\x7\<Esc>\\"
let &t_8f="\<Esc>[38;2;%lu;%lu;%lum"
let &t_8b="\<Esc>[48;2;%lu;%lu;%lum"

" ================================================
" Indentation
" ================================================
set smarttab
set shiftwidth=2
set softtabstop=2
set tabstop=2
set expandtab
set smartindent
set autoindent

" ================================================
" Completion
" ================================================
set wildmode=list:full
set wildmenu
set wildignore=*.o,*.obj,*~
set wildignore+=*vim/backups*
set wildignore+=*sass-cache*
set wildignore+=*vendor/**
set wildignore+=*node_modules/**
set wildignore+=*DS_Store*
set wildignore+=*.gem
set wildignore+=log/**
set wildignore+=tmp/**
set wildignore+=*.png,*.jpg,*.gif

" ================================================
" Swap files
" ================================================
set noswapfile
set nobackup
set nowb

" ================================================
" Functions
" ================================================
function! FixFile()
    let l = line(".")
    let c = col(".")
    " Fix indentation
    exec "normal! gg=G"
    " Delete trailing slashes
    %s/\s\+$//e
    " Replace tabs with spaces
    %s/\t/    /e
    call cursor(l, c)
    set expandtab
endfunction

let $unrol=1
function! ToggleFold()
    if $unrol==0
        :exe "normal! zR"
        let $unrol=1
    else
        :Fs<CR>
        let $unrol=0
    endif
endfunction

function! ToggleSyntax()
    if (exists("g:syntax_on"))
        syntax off
    else
        syntax enable
    endif
endfunction

function! NERDTreeToggleInCurDir()
    " If NERDTree is open in the current buffer
    if (exists("t:NERDTreeBufName") && bufwinnr(t:NERDTreeBufName) != -1)
        exe ":NERDTreeClose"
    else
        if (expand("%:t") != '')
            exe ":NERDTreeFind"
        else
            exe ":NERDTreeToggle"
        endif
    endif
endfunction

" Save cursor position when switching buffers
au BufLeave * let b:winview = winsaveview()
au BufEnter * if(exists('b:winview')) | call winrestview(b:winview) | endif

" Autoread
augroup checktime
    au!
    if !has("gui_running")
        "silent! necessary otherwise throws errors when using command
        "line window.
        autocmd BufEnter        * silent! checktime
        autocmd CursorHold      * silent! checktime
        autocmd CursorHoldI     * silent! checktime
        "these two _may_ slow things down. Remove if they do.
        autocmd CursorMoved     * silent! checktime
        autocmd CursorMovedI    * silent! checktime
    endif
augroup END

" ================================================
" Abbreviations and insert mode snippets
" ================================================
iabbrev cl console.log();<Left><Left>
inoremap cl<tab> console.log();<Left><Left>
inoremap ,, <Esc>A,<Esc>
inoremap ;; <Esc>A;<Esc>

" ================================================
" Key mappings (bindings)
" ================================================
" Map leader
let mapleader = "\<Space>"

" Reload file
nnoremap <Leader>r :e!<CR>

" Reload vim config
nnoremap <Leader><Leader>r :source ~/.vimrc<CR>

" ToggleSyntax
nnoremap <Leader><Leader>s :call ToggleSyntax()<CR>

" Create file with gf
map gf :edit <cfile><CR>

" Paste and copy pasted
xnoremap <Leader>v "0p

" Chords
nnoremap <Leader><Leader>c magg:vsplit<CR>:set noscrollbind<CR><C-w>l<C-f>:set scrollbind<CR><C-w>h:set scrollbind<CR>`a

" Close splits
nnoremap = :only<CR>

" Format json
nnoremap <Leader><Leader>j :%!python -m json.tool<CR>
vnoremap <Leader><Leader>j :!python -m json.tool<CR>

" Disable Ex mode
noremap Q <NOP>

" Copy file path
nnoremap <Leader>y :let @* = expand("%")<CR>

" Copy to the end of the line
nnoremap Y y$

" Line start and end
nnoremap L $
vnoremap L $h
nnoremap H ^
vnoremap H ^

" Quickfix and location lists
map <Leader>co :copen<CR>
map <Leader>cc :cclose<CR>
map <Leader>cn :cn<CR>
map <Leader>cp :cp<CR>
map <Leader>lo :lopen<CR>
map <Leader>lc :lclose<CR>

" Search selected
vnoremap // y/<C-R>"<CR>N

" Move selected
vnoremap J :m '>+1<CR>gv=gv
vnoremap K :m '<-2<CR>gv=gv

" Search under the cursor and back to that instance
nnoremap \ *N
" Search under the cursor and change it
nnoremap ' *Ncgn

" Easy command mode
nnoremap ; :
vnoremap ; :

" Switch between buffers quickly
map <Leader><Tab> <C-^>

" Delete all buffers
nnoremap <Leader>Q :bufdo bdelete<CR>

" Delete buffer
nnoremap <Leader>x :bn<CR>:bd#<CR>

" Down is really the next line
nnoremap j gj
nnoremap k gk

" Delete content of the file
nnoremap <Leader>d ggdG
" Copy content of the file
nnoremap <Leader><Leader>y mpggVGy`p

" Add empty line and stay in command mode
nmap <Leader>j o<Esc>k
nmap <Leader>k O<Esc>j

" Save previous position
nnoremap gg mpgg
nnoremap G mpG
nnoremap / mp/

" Fast exit to normal mode
inoremap jj <Esc>
inoremap jk <Esc>

" Substitute
nnoremap <Leader>sr :%s///<left><left><C-f>i

" Fast save
nnoremap <Leader>w :w<CR>

" Fast quit
nnoremap <Leader>q :q<CR>

" Shorthand for making window smaller and bigger
nnoremap <Leader>, <C-w>10<
nnoremap <Leader>. <C-w>10>

" Hide highlight
nnoremap <silent> <BS> :noh<CR>

" Indenting in visual and normal mode
xnoremap <s-tab> <gv
xnoremap <tab> >gv
nnoremap <tab> >>
nnoremap <s-tab> <<

" Clear trailing whitespaces
nnoremap <Leader>i :call FixFile()<CR>

" Vue mappings
nnoremap <Leader>1 :e %<.vue<CR>
nnoremap <Leader>2 :e %<.js<CR>
nnoremap <Leader>3 :e %<.css<CR>
" ================================================
" Plugin settings and mappings (bindings)
" ================================================

" NERDTree
nnoremap - :call NERDTreeToggleInCurDir()<CR>
let NERDTreeIgnore=['\.git$']
let NERDTreeMinimalUI = 1
let NERDTreeShowHidden=1

" Fzf
if isdirectory(".git")
    nnoremap <Leader>p :GFiles!<CR>
else
    nnoremap <Leader>p :Files!<CR>
endif
nnoremap <Leader>o :Buffers!<CR>
nnoremap <Leader><Leader>p :Files!<CR>
nnoremap <Leader>gf :Commits!<CR>
nnoremap <Leader>gh :BCommits!<CR>
nnoremap <Leader>m :Maps!<CR>
nnoremap <Leader>c :Commands!<CR>
nnoremap <Leader>\ :Filetypes!<CR>

function! RipgrepFzf(query, fullscreen)
  let command_fmt = 'rg --column --line-number --no-heading --color=always --smart-case -g !package-lock.json -- %s || true'
  let initial_command = printf(command_fmt, shellescape(a:query))
  let reload_command = printf(command_fmt, '{q}')
  let spec = {'options': ['--phony', '--query', a:query, '--bind', 'change:reload:'.reload_command]}
  call fzf#vim#grep(initial_command, 1, fzf#vim#with_preview(spec), a:fullscreen)
endfunction

command! -nargs=* -bang RG call RipgrepFzf(<q-args>, <bang>0)
nnoremap <Leader><Leader>f :RG<CR>

inoremap <expr> <c-p> fzf#vim#complete#path('rg --files')
inoremap <expr> <c-k> fzf#vim#complete#word()

" Fugitive commands
nnoremap <Leader>gst :Git<CR>
nnoremap <Leader>gd :Gdiff<CR>
nnoremap <Leader>gb :Git blame<CR>
nnoremap <C-n> :echo fugitive#statusline()<CR>
nnoremap <Leader>gco :Gread<CR>:w<CR>

" Rg Search
let g:ackprg = 'rg -F -S --color=never --no-heading --with-filename --line-number --column -g !package-lock.json'
nmap <Leader>f :Ack! "" ./<Left><Left><Left><Left>
nnoremap <Leader>a :Ack! <cword><CR>

" Fold searches
let g:foldsearch_disable_mappings=1
nnoremap , :call ToggleFold()<CR>

" Center highlighted search and show n/N
nnoremap n nzz
nnoremap N Nzz

" Ale
let g:ale_fix_on_save = 1
let g:ale_sign_column_always = 1
let g:ale_hover_to_floating_preview = 1
let g:ale_detail_to_floating_preview = 1
let g:ale_set_highlights = 0
let g:ale_floating_preview = 1
let g:ale_virtualtext_cursor = 1
let g:ale_floating_window_border = ['│', '─', '╭', '╮', '╯', '╰']
" let g:ale_sign_error = '✘❌✖☠'
" let g:ale_sign_warning = '⚠️'⚠
let g:ale_sign_error = "◉"
let g:ale_sign_warning = "◉"
highlight ALEErrorSign ctermbg=NONE ctermfg=red
highlight ALEWarningSign ctermbg=NONE ctermfg=yellow
autocmd FileType javascript let g:ale_linters = {
\  'javascript': glob('.eslintrc*', '.;') != '' ? [ 'eslint' ] : [ 'standard' ],
\}

let g:ale_fixers = {
\  'javascript': glob('.eslintrc*', '.;') != '' ? [ 'eslint' ] : [ 'standard' ],
\  'typescript': [ 'eslint' ],
\  '*': ['remove_trailing_lines', 'trim_whitespace']
\}
nnoremap <silent>gd :ALEGoToDefinition<CR>
nnoremap <silent>K :ALEHover<CR>
nnoremap <silent>gr :ALEFindReferences<CR>
nnoremap <Leader>gn :ALENext<CR>
nnoremap <Leader>gp :ALEPrevious<CR>
nnoremap <Leader>gr :ALERename<CR>
nnoremap <Leader>g, :ALECodeAction<CR>

" Easymotion
nmap s <Plug>(easymotion-overwin-w)
" nmap S <Plug>(easymotion-overwin-f2)
let g:EasyMotion_smartcase = 1

" Split join
let g:splitjoin_html_attributes_bracket_on_new_line = 1

" Vimwiki
let g:vimwiki_list = [{'path': '~/My Drive/Notes/', 'syntax': 'markdown', 'ext': '.md'}]
let g:vimwiki_map_prefix = '<Leader>n'
nnoremap <Leader><CR> <Plug>VimwikiToggleListItem
nnoremap <F5> 0f]l"=strftime("(%d.%m.%y. %H:%M:%S) ")<CR>p
let g:vimwiki_key_mappings =
    \ {
    \ 'headers': 0
      \ }
" Goyo
" autocmd BufRead,BufNewFile *.md :Goyo

" Devicons
if exists("g:loaded_webdevicons")
	call webdevicons#refresh()
endif
